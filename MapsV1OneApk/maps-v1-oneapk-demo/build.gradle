/*
 * Copyright (c) 2014 Nokia Corporation and/or its subsidiary(-ies).
 * See the license text file delivered with this project for more information.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion "Google Inc.:Google APIs:16"
    buildToolsVersion "19.0.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'MissingRegistered'
    }
}

/*
// To enable release builds with signing, add the following lines to your .gradle/gradle.properties
STORE_FILE=/path/to/your.keystore
STORE_PASSWORD=yourkeystorepass
KEY_ALIAS=projectkeyalias
KEY_PASSWORD=keyaliaspassword
 */
if (project.hasProperty('STORE_FILE') &&
        project.hasProperty('STORE_PASSWORD') &&
        project.hasProperty('KEY_PASSWORD') &&
        project.hasProperty('KEY_ALIAS')) {
    android.signingConfigs.release.storeFile = file(STORE_FILE)
    android.signingConfigs.release.storePassword = STORE_PASSWORD
    android.signingConfigs.release.keyPassword = KEY_PASSWORD
    android.signingConfigs.release.keyAlias = KEY_ALIAS
    android.buildTypes.release.signingConfig = android.signingConfigs.release
} else {
    android.buildTypes.release.signingConfig = null
}


dependencies {

    // You must install or update the Google Repository through the SDK manager to use this dependency.
    // The Google Repository (separate from the corresponding library) can be found in the Extras category.
    // compile 'com.google.android.gms:play-services:4.0.+'
    compile 'com.android.support:support-v4:+'
    compile files('../nokia-x-here-maps-wrapper-v1/com.nokia.android.maps.jar')
}
